def serif(suffix $) (expr width, height) =
  pos$a(height, 90);
  pos$b(height, 90);
  pos$c(height, 90);
  lft x$a = x$b - 1/2width = rt x$c - width;
  bot y$a.l = bot y$c.l = bot y$b.l - dish;
enddef;

def serifpath(suffix $) =
  z$a.r{right}..z$b.r{right}..{right}z$c.r--
  z$c.l{left}..z$b.l{left}..{left}z$a.l--cycle;
enddef;

def topserif(suffix $) (expr width, height) =
  pos$a(height, 90);
  pos$b(height, 90);
  lft x$a = x$b - 1/2width;
  bot y$a.l = bot y$b.l + dish;
enddef;

def trunk(suffix $) (expr jut, stem, height) =
  serif($b, jut*2+stem+currentbreadth, slab);
  topserif($t, jut*2+stem, slab);
  pos$c(stem, 0);
  x$b.a.l = x$t.a.l;
  y$tl = height - slab/2;
  top y$t.a.r = top y$tr = height;
  lft x$c.l = lft x$tl = x$b.a + jut;
  y$c.l = y$b.a.r;
  x$tr = x$tl + stem - currentbreadth;
enddef;

def filltrunk(suffix $) =
  filldraw serifpath($b);
  filldraw z$t.a.r{right}..z$t.b.r{right}..{right}z$tr--z$t.b.l{left}..{left}z$t.a.l--cycle;
  filldraw z$tl{right}--z$tr--z$c.r--z$c.l--cycle;

  penlabels($b.a, $b.c, $t.a, $t.b, $c, $tl, $tr);
enddef;

beginchar("c", 12u#, x_height#, dp#);
  pickup tiny.nib;
  pos1(vair, 158);
  pos3(curve, 0);
  pos4(vair, -90);
  pos5(flare, flareangle);
  
  top y1r = 0.35x_height;
  rt x1l = x5r + 0.04w;
  
  x2r = 0.45[rt x3r, lft x1r];
  bot y2l = -o;
  top y2r = bot y2l + 0.05 [hair, stem];

  lft x3l = 0;
  y3 = x_height/2;

  x4 = x2l = w/2;
  top y4l = x_height + o;
  
  lft x5l = 0.2[w - curve, x4r];
  bot y6 = 0.66h;
  z6 = z5 + 0.55flare * dir (flareangle - 90);

  penlabels(1,2,3,4,5,6);

  filldraw z1l{dir -100}..tension 0.8 and 1..z2l{left}..tension 0.88..z3l{up}..tension 0.88..
  z4l{right}..tension 1.1 and 1.1..z5r{dir (flareangle - 90)}..z6{left}..z5l{dir (flareangle + 90)}..tension 0.85..
  z4r{left}..tension 0.88..z3r{down}..tension 0.88..z2r{right}..tension 0.85 and 1.1..{dir 80}z1r--cycle;
endchar;

beginchar("e", 12u#, x_height#, dp#);
  pickup tiny.nib;
  pos1(vair, 158);
  pos3(curve, 0);
  pos4(vair, -90);
  pos5(curve, 180);
  pos6(vair, 90);

  top y1r = 0.32x_height;
  rt x1l = 0.98w;
  
  x2r = 0.45[rt x3r, lft x1r];
  bot y2l = -o;
  top y2r = bot y2l + 0.05 [hair, stem];

  lft x3l = 0;
  y3 = 1/2x_height;

  x4l = x2l = w/2;
  top y4l = x_height + o;
  
  rt x5l = 0.98w;
  bot y5 = y6 - vair/2;
  
  x6 = stem/2;
  y6 = 0.60x_height;
  penlabels(1,2,3,4,5,6);

  filldraw z1l{dir -108}..tension 0.8 and 1..z2l{left}..tension 0.88..z3l{up}..tension 0.88..
  z4l{right}..tension 0.95 and 0.75..z5l{down}--z6l--z6r--(x5r,y6r){dir 92}..z4r{left}..tension 0.87..
  z3r{down}..tension 0.87..z2r{right}..tension 0.85 and 1.1..{dir 72}z1r--cycle;
endchar;

beginchar("i", jut#*2+stem#+fine#, x_height#, dp#);
  pickup crisp.nib;
  trunk(1, jut, stem, x_height);
  lft x1b.a.l = 0;
  bot y1b.a.l = 0;
  filltrunk(1);
  draw unitcircle scaled
endchar;

beginchar("o", 12u#, x_height#, dp#);
  pickup tiny.nib;
  pos1(curve, 0);
  pos2(vair, 270);
  pos3(curve, 180);
  pos4(vair, 90);
  
  lft x3r = 0;
  bot y2r = -o;
  rt x1r = w;
  top y4r = x_height+o;
  x2 = x4 = w/2;
  y1 = y3 = x_height/2;
  penlabels(1,2,3,4);

  filldraw z1r{down}..tension 0.88..z2r{left}..tension 0.88..z3r{up}..tension 0.88..z4r{right}..tension 0.88..{down}z1r--
  z1l{up}..tension 0.88..z4l{left}..tension 0.88..z3l{down}..tension 0.88..z2l{right}..tension 0.88..{up}z1l--cycle;
endchar;
end